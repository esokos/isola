      SUBROUTINE AUXPLN (DD1, DA1, SA1, DD2, DA2, SA2)
C
C    CALCULATE THE AUXILLIARY PLANE OF A DOUBLE COUPLE FAULT PLANE SOLUTION, GIVEN THE PRINCIPLE PLANE.
C
C    WRITTEN BY PAUL REASENBERG, JUNE, 1984, FROM CLASS NOTES BY DAVE BOORE, (BOTH AT THE U.S.G.S., MENLO PARK.)
C    ANGLE VARIABLES PHI, DEL AND LAM ARE AS DEFINED IN AKI AND RICHARDS, (1980), P.114.
C
      REAL              DA1
*       ! (INPUT)  DIP ANGLE IN DEGREES OF PRICIPLE PLANE
      REAL              DD1
*       ! (INPUT)  DIP DIRECTIONS IN DEGREES OF PRICIPLE PLANE
      REAL              SA1
*       ! (INPUT)  SLIP ANGLE IN DEGREES OF PRICIPLE PLANE
      REAL              DA2
*       ! (OUTPUT)  DIP ANGLE IN DEGREES OF AUXILLIARY PLANE
      REAL              DD2
*       ! (OUTPUT)  DIP DIRECTIONS IN DEGREES OF AUXILLIARY PLANE
      REAL              SA2
*       ! (OUTPUT)  SLIP ANGLE IN DEGREES OF AUXILLIARY PLANE
C

      DOUBLE PRECISION  BOT
*       ! SCRATCH VARIABLE
      DOUBLE PRECISION  DEL1
*       ! DIP ANGLE OF PRINCIPAL PLANE IN RADIANS
      LOGICAL           FIRST
*       ! TEST: TRUE IF FIRST TIME INTO ROUTINE
      DOUBLE PRECISION  PHI1
*       ! FAULT PLANE STRIKE OF PRINCIPAL PLANE
      DOUBLE PRECISION  PHI2
*       ! STRIKE OF AUXILLIARY PLANE IN RADIANS
      DOUBLE PRECISION  RAD
*       ! CONVERSION FACTOR FROM DEGREES TO RADIAN
      DOUBLE PRECISION  SGN
*       ! SAVES PRINCIPAL PLANE SLIP ANGLE FOR ASSIGNING PROPER SIGN TO AUXILLIARY
      DOUBLE PRECISION  TOP
*       ! SCRATCH VARIABLE
      DOUBLE PRECISION  XLAM1
*       ! SLIP ANGLE OF PRINCIPAL PLANE IN RADIANS
      DOUBLE PRECISION  XLAM2
*       ! SLIP ANGLE OF AUXILLIARY PLANE
C
      SAVE FIRST, RAD
      DATA FIRST /.TRUE./
C
      IF (FIRST) THEN
        FIRST = .FALSE.
        RAD = DATAN(1.0D0)/45.0D0
      END IF
C
      PHI1 = DD1 - 90.0D0
      IF (PHI1 .LT. 0.0D0) PHI1 = PHI1 + 360.0D0
      PHI1 = PHI1*RAD
      DEL1 = DA1*RAD
      SGN = SA1
      XLAM1 = SA1*RAD
C
      TOP = DCOS(XLAM1)*DSIN(PHI1) - DCOS(DEL1)*DSIN(XLAM1)*DCOS(PHI1)
      BOT = DCOS(XLAM1)*DCOS(PHI1) + DCOS(DEL1)*DSIN(XLAM1)*DSIN(PHI1)
      DD2 = DATAN2(TOP, BOT)/RAD
      PHI2 = (DD2 - 90.0D0)*RAD
      IF (SA1 .LT. 0.0D0) DD2 = DD2 - 180.0D0
      IF (DD2 .LT. 0.0D0) DD2 = DD2 + 360.0D0
      IF (DD2. GT. 360.0D0) DD2 = DD2 - 360.0D0
C
      DA2 = DACOS(DSIN(DABS(XLAM1))*DSIN(DEL1))/RAD
      XLAM2 = -DCOS(PHI2)*DSIN(DEL1)*DSIN(PHI1) +
     & DSIN(PHI2)*DSIN(DEL1)*DCOS(PHI1)
C
C MACHINE ACCURACY PROBLEM
C
      IF (DABS(XLAM2) .GT. 1.0D0) THEN
        XLAM2 = DSIGN(1.0D0, XLAM2)
      END IF
      XLAM2 = DSIGN(DACOS(XLAM2), SGN)
      SA2 = XLAM2/RAD
C
      RETURN
      END

